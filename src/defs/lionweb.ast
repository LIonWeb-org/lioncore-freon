language LIonCoreM3

model LIonCoreM3 {
    name: identifier;
    units: Language[];
}

interface INamed {
    name: identifier;
}

interface IKeyed base INamed {
    key: string;
}

modelunit Language implements IKeyed {
    version: string;
    entities: LanguageEntity[];
    reference dependsOn: Language[];
    // Freon specific addition
    file-extension = "lmm";
}

abstract concept LanguageEntity implements IKeyed {
}

concept Property base Feature {
    reference type: Datatype;
}

abstract concept Feature implements IKeyed {
  optional: boolean;
}

abstract concept Classifier base LanguageEntity  {
    features: Feature[];
}

concept Concept base Classifier {
    abstract: boolean;
    partition: boolean;
    reference extends?: Concept;
    reference implements: ConceptInterface[];
}

abstract concept Link base Feature  {
    multiple: boolean;
    reference type: Classifier;
}

concept ConceptInterface  base Classifier {
    reference extends: ConceptInterface[];
}

concept Containment  base Link { }

concept Reference base Link {
 }

abstract concept Datatype base LanguageEntity {
}

limited PrimitiveType base Datatype {
//    key: string;
    Boolean = { name: "Boolean", key: "LIonCore-builtins-Boolean" }
    String = { name: "String", key: "LIonCore-builtins-String" }
    Integer = { name: "Integer", key: "LIonCore-builtins-Integer" }
    JSON = { name: "JSON", key: "LIonCore-builtins-JSON" }
}

concept Enumeration base Datatype {
    literals: EnumerationLiteral[];
}

concept EnumerationLiteral implements IKeyed {
}

